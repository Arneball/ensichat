apply plugin: 'com.android.application'
apply plugin: 'jp.leafytree.android-scala'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "jp.leafytree.gradle:gradle-android-scala-plugin:1.3.1"
    }
}

dependencies {
    compile "com.android.support:support-v4:21.0.0"
    compile "org.msgpack:msgpack-scala_2.11:0.6.11"
    compile "com.android.support:multidex:1.0.0"
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        applicationId "com.nutomic.ensichat"
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 1
        versionName "0.1.0"
        testInstrumentationRunner "com.android.test.runner.MultiDexTestRunner"
    }

    sourceSets {
        main {
            scala {
                srcDir "src/main/scala"
            }
        }

        androidTest {
            scala {
                srcDir "src/androidTest/scala"
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            dependencies {
                provided "org.scala-lang:scala-library:2.11.4"
            }
        }
        release {
            minifyEnabled true
            proguardFile file("proguard-rules.pro")
            dependencies {
                compile "org.scala-lang:scala-library:2.11.4"
            }
        }
    }

    // Avoid duplicate file errors during packaging.
    packagingOptions {
        exclude 'decoder.properties'
        exclude 'rootdoc.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    dexOptions {
        preDexLibraries false
    }
}

afterEvaluate {
    tasks.matching {
        it.name.startsWith("dex")
    }.each { dx ->
        if (dx.additionalParameters == null) {
            dx.additionalParameters = []
        }
        dx.additionalParameters += "--multi-dex"
        dx.additionalParameters += "--main-dex-list=$rootDir/main-dex-list.txt".toString()
    }
}
